plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.3.0'
    id 'com.google.protobuf' version '0.9.5'
    id 'me.champeau.jmh' version '0.7.1'
    id 'io.morethan.jmhreport' version '0.9.0'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

dependencies {
    implementation 'org.apache.avro:avro:1.11.1'
    implementation 'com.google.protobuf:protobuf-java:3.25.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'


    implementation 'org.openjdk.jmh:jmh-core:1.37'
    annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = 'com.gsswain.benchmark.serialization.SerializationReportGenerator'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/model/**',
                'com/gsswain/benchmark/serialization/SerializationReportGenerator.class',
                'com/gsswain/benchmark/serialization/SerdeProcess.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                '*.model.**',
                'com.gsswain.benchmark.serialization.SerializationReportGenerator',
                'com.gsswain.benchmark.serialization.SerdeProcess'
            ]
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

build.dependsOn test

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'build/generated/source/proto/main/java', 'build/generated/sources/avro/java/main'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.1'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}

jmh {
    resultFormat = 'JSON'
    resultsFile = layout.buildDirectory.file('reports/jmh/results.json').get().asFile
    jmhVersion = '1.37'
    timeUnit = 'ns'
    threads = project.hasProperty('jmh.threads') ? project.property('jmh.threads').toInteger() : 1
}

tasks.named('jmhReport') {
    dependsOn('jmh')
}
